package blang.distributions

/** Arbitrary linear transformations of \(n\) iid standard normal random variables. */
model MultivariateNormal {
  random Matrix realization
  
  /** An \(n \times 1\) vector \(\mu\). \(\mu \in \mathbb{R}^n\)  */
  param Matrix mean
  // Note: no need to mark as constrained since CholeskyDecomposition is read-only 
  // so won't attempt to do naive sampling by default
  /** Inverse covariance matrix \(\Lambda\), a positive definite \(n \times n\) matrix. */
  param CholeskyDecomposition precision
  
  laws {
    logf(double dim = realization.nEntries) {
      - dim * log(2*PI) / 2.0
    }
    logf(precision) {
      0.5 * precision.logDet
    }
    logf(mean, precision, realization)  {
      val Matrix centered = mean - realization
      val Matrix L = precision.L
      // by doing left to right this is quadratic not cubic:  
      return - 0.5 * (centered.transpose * L * L.transpose * centered).doubleValue
    }
  }
  
  generate(rand) {
    realization.setTo(rand.multivariateNormal(mean, precision))
  }
}