package blang.distributions

/** Student T random variable. Values in \(\mathbb{R}\) */
model StudentT {
  random RealVar realization
  
  /** The degrees of freedom \(\nu\). \( \nu > 0 \) */
  param RealVar nu
  
  /** Location parameter \(\mu\). \(\mu \in \mathbb{R}\) */
  param RealVar mu
  
  /** Scale parameter \(\sigma\). \(\sigma > 0\) */
  param RealVar sigma
  
  laws{
    logf(){
        return - 0.5 * log(PI)
    }

    logf(sigma){
      if (sigma <= 0.0) return NEGATIVE_INFINITY
      return -log(sigma)
    }
    
    logf(nu){
      if (nu <= 0.0) return NEGATIVE_INFINITY
      return lnGamma((nu + 1.0) / 2.0) - 0.5 * log(nu) - lnGamma(nu / 2.0) 
    }
    
    logf(mu, nu, sigma, realization){
      if (nu <= 0.0 || sigma <= 0.0) return NEGATIVE_INFINITY
      return - ((nu + 1.0) / 2.0) * log(1.0 + (1.0 / nu) * ( 1.0 / (pow(sigma, 2)) ) * pow((realization - mu), 2))
    }
     
  }
  
  generate(rand){
    rand.studentt(nu, mu, sigma)
  }
}